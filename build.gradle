// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.hb.jetgithub.buildsrc.*

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath ProjectDependencies.androidGradlePlugin
        classpath ProjectDependencies.gradlePlugin
        classpath ProjectDependencies.extensions
        classpath ProjectDependencies.daggerHiltClasspath
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.1.1'
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
            // Opt-in to experimental compose APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += "-Xallow-jvm-ir-dependencies"
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            ktlint(Versions.ktlint)
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}